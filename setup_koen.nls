; --- Global variables ---
globals [
  width         ; width of the court
  height        ; height of the court
  time
  points-lakers
  points-celtics
  ball-position
  distance-for-possession ; determines the distance a player must have from the ball to have possesion of it
  basket1-pos ; position 1 to score
  basket2-pos
  inbound1 ; position 1 to inbound
  inbound2
  shot-made ; team that scored or false
  shot-missed ; team that missed or false
  loose-ball?
  vision-distance
  target
  closest-to-ball
  speed-with-ball
]

; --- Agents ---
breed [players player]
breed [balls ball]
breed [referees referee]

; --- Local variables ---
; beliefs are: ball, team-has-ball, team, basket-to-score, if the player has the ball, ...
players-own[
  desire
  intention
  team
  basket-to-score
  player-has-ball?
  team-has-ball?
  is-open?
  shooting-range
  in-shooting-range?
  place-to-inbound
  players-open
  best-option
  spot
  spot-defense;;;;;;
  getting-to-offensive-spot?
  getting-to-defensive-spot?
  got-back?
  ;change-of-possesion?
  defends-ball? ; true or false for one player of the team
  ball-is-defended? ; true or false for the whole team
  defender-is-close?
  received-messages
  team-had-ball? ; to make sure that the team has the ball (in some cases it is behind one step)
]

balls-own[
  owner
  ball-passed?
]

referees-own [
  team
]

; --- Setup the lines and the baskets of the court ---
to setup-court
  set width 50 / 2
  set height 94 / 2
  setup-lines
  set basket1-pos patches with [pxcor = height - 4 and pycor = 0]
  set basket2-pos patches with [pxcor = -1 * height + 4 and pycor = 0]
  set inbound1 patches with [pxcor = height + 1 and pycor = 10]
  set inbound2 patches with [pxcor = -1 * height - 1 and pycor = 10]
end

to setup-lines ; set the lines
  ask patches [
    set pcolor 37
  ]
  color-lines outer-lines white
  color-lines middle-line black + 2
  color-lines basket1 red
  color-lines basket2 red
  color-lines basket-base gray
  color-lines three-point-line1 black + 2
  color-lines three-point-line2 black + 2
  color-lines middle-circle white
  color-lines paint black + 2
end

to color-lines [line col]
  let lines line
  ask lines [
    set pcolor col
  ]
end

to-report outer-lines
  report patches with [pycor = width or pycor = -1 * width or pxcor = height or pxcor = -1 * height
    and pycor <= width and pycor >= -1 * width and pxcor <= height and pxcor >= -1 * height]
end

to-report middle-line
  report patches with [pxcor = 0 and pycor <= width - 1 and pycor >= -1 * width + 1]
end

to-report basket1
  report patches with [(pycor = -1 or pycor = 1) and (pxcor = height - 3 or pxcor = height - 4 or pxcor = height - 5)
     or (pycor = 0 and (pxcor = height - 3 or pxcor = height - 5))]
end

to-report basket2
  report patches with [(pycor = -1 or pycor = 1) and (pxcor = -1 * height + 3 or pxcor = -1 * height + 4 or pxcor = -1 * height + 5)
     or (pycor = 0 and (pxcor = -1 * height + 3 or pxcor = -1 * height + 5))]
end

to-report basket-base
  report patches with [pycor = 0 and (pxcor = height - 2 or pxcor = height - 1 or
    pxcor = -1 * height + 2 or pxcor = -1 * height + 1)]
end

to-report three-point-line1
  report patches with [pxcor >= height - 23 and pxcor <= height - 1 and (pycor = width - 4 or pycor = -1 * width + 4) or
    (pxcor = height - 24 and pycor <=  width - 4 and pycor >=  -1 * width + 4)]
end

to-report three-point-line2
  report patches with [pxcor <= -1 * height + 23 and pxcor >= -1 * height + 1 and (pycor = width - 4 or pycor = -1 * width + 4) or
    (pxcor = -1 * height + 24 and pycor <=  width - 4 and pycor >=  -1 * width + 4)]
end

to-report middle-circle
  report patches with [pxcor >= -4 and pxcor <= 4 and (pycor = -4 or pycor = 4) or
    (pxcor = -4 or pxcor = 4) and (pycor >= -4 and pycor <= 4)]
end

to-report paint
  report patches with [pxcor = height - 15 or pxcor = -1 * height + 15 and pycor >= -4 and pycor <= 4 or 
    (pycor = -5 or pycor = 5 and ((pxcor >= height - 15 and pxcor <= height - 1) or (pxcor <= -1 * height + 15 and pxcor >= -1 * height + 1)))]
end

; --- Setup the players and the ball ---
to setup-game
  set-default-shape players "player"
  set-default-shape balls "ball basketball"

  create-players 5 [
    set team "celtics"
    set color green
    set basket-to-score basket2-pos
    set place-to-inbound inbound1
    ;set basket-to-defend basket1-pos
  ]

  create-players 5 [
    set team "lakers"
    set color yellow
    set basket-to-score basket1-pos
    set place-to-inbound inbound2
    ;set basket-to-defend basket2-pos
  ]

  pos-player 0 2 0
  pos-player 1 5 -5
  pos-player 2 5 5
  pos-player 3 7 0
  pos-player 4 12 0
  pos-player 5 -2 0
  pos-player 6 -5 -5
  pos-player 7 -5 5
  pos-player 8 -7 0
  pos-player 9 -12 0

  ask players [
     facexy 0 0
     set size 5
     set team-has-ball? false
     set team-had-ball? false
     set shooting-range 15 ; all have the same shooting range for now
     set in-shooting-range? false
     set players-open []
     set getting-to-offensive-spot? false
     set getting-to-defensive-spot? false
     ;set spot [0 0]
     set got-back? false
     ;set change-of-possesion? false
     set player-has-ball? false
     set defends-ball? false
     set ball-is-defended? false
     set defender-is-close? false
     set received-messages []
  ]

  create-referees 1 [
    hide-turtle
  ]

  create-balls 1 [
    set size 2
    set color orange
    setxy 0 0
    set owner referee 10
    set ball-position self
    set ball-passed? false
  ]

  set points-lakers 0
  set points-celtics 0
end

to pos-player [number x y]
  ask player number [ setxy x y ]
end

; --- Setup global parameters ---
to setup-parameters
  set distance-for-possession 3
  set time 0
  set shot-made "false"
  set shot-missed "false"
  set loose-ball? true
  set vision-distance 50
  set speed-with-ball 0.8
end

; --- Setup ticks ---
to setup-ticks
  reset-ticks
end